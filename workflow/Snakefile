from snakemake.utils import min_version
min_version("7.0")

configfile: "config/config.yaml"
include: "rules/common.smk"
include: "rules/sumstats.smk"
include: "rules/fastq.smk"
include: "rules/reference.smk"
include: "rules/mappability.smk"

if config['sentieon']:
    include: "rules/sentieon.smk"
else:
    include: "rules/fastq2bam.smk"
    if config['intervals']:
        include: "rules/bam2vcf_gatk_intervals.smk"
        include: "rules/intervals.smk"
    else:
        include: "rules/bam2vcf_gatk.smk"

if config['cov_filter']:
    include: "rules/cov_filter.smk"

if config['remote_reads']:
    from snakemake.remote.GS import RemoteProvider as GSRemoteProvider
    GS = GSRemoteProvider()
    GS_READS_PREFIX = config['remote_reads_prefix']

module qc:
    snakefile:
        "modules/qc/Snakefile"
    config:
        config

use rule * from qc as qc_*

module mk:
    snakefile:
        "modules/mk/Snakefile"
    config:
        config

use rule * from mk as mk_*

module postprocess:
    snakefile:
        "modules/postprocess/Snakefile"
    config:
        config

use rule * from postprocess as postprocess_*

rule all:
    input:
        get_output()
    default_target: True
